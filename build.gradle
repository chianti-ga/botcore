plugins {
    id 'java'
    id 'idea'
    //Update deps
    id "com.github.ben-manes.versions" version "0.46.0"

    id "io.freefair.lombok" version "8.2.1"

    id 'java-library'
    id 'maven-publish'
}

group 'fr.skitou.botcore'

repositories {
    mavenCentral()
    maven {
        url "https://plugins.gradle.org/m2/"
    }
    maven {
        url 'https://jitpack.io'
    }
}
allprojects {

    dependencies {
        implementation 'com.google.guava:guava:32.1.2-jre'

        //===Unit tests===

        testImplementation('org.junit.jupiter:junit-jupiter-api:5.10.0')
        testRuntimeOnly('org.junit.jupiter:junit-jupiter-engine:5.10.0')

        testImplementation 'org.mockito:mockito-core:5.5.0'
        // https://mvnrepository.com/artifact/org.mockito/mockito-junit-jupiter
        testImplementation group: 'org.mockito', name: 'mockito-junit-jupiter', version: '5.4.0'

        // === APIs ===
        // JDA
        implementation("net.dv8tion:JDA:5.0.0-beta.13")

        // === Logging ==

        // https://mvnrepository.com/artifact/org.slf4j/slf4j-api
        implementation group: 'org.slf4j', name: 'slf4j-api', version: '2.0.5'
        // https://mvnrepository.com/artifact/ch.qos.logback/logback
        implementation group: 'ch.qos.logback', name: 'logback-classic', version: '1.4.11'

        // Utils
        implementation group: 'org.reflections', name: 'reflections', version: '0.10.2'

        implementation group: 'org.jetbrains', name: 'annotations', version: '24.0.1'
        implementation 'org.apache.commons:commons-lang3:3.13.0'

        implementation 'com.google.code.gson:gson:2.10.1'

        // https://mvnrepository.com/artifact/org.apache.commons/commons-lang3
        implementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.13.0'
        // https://mvnrepository.com/artifact/commons-io/commons-io
        implementation group: 'commons-io', name: 'commons-io', version: '2.13.0'

        //Java Persistence API
        implementation('org.hibernate.orm:hibernate-core:6.2.7.Final')
        implementation 'org.hibernate.orm:hibernate-c3p0:6.3.0.CR1'
        implementation 'org.hibernate.orm:hibernate-community-dialects:6.3.0.CR1'
        implementation group: 'org.xerial', name: 'sqlite-jdbc', version: '3.42.0.1'

        implementation 'com.vdurmont:emoji-java:5.1.1'

        implementation 'io.sentry:sentry:6.28.0'

        implementation group: 'club.minnced', name: 'discord-webhooks', version: '0.8.4'
    }

    idea {
        module {
            downloadJavadoc = true
        }
    }
}

tasks.withType(AbstractArchiveTask) {
    preserveFileTimestamps = false
    reproducibleFileOrder = true
}

tasks.withType(JavaCompile) {
    options.encoding('UTF-8')
}

test {
    useJUnitPlatform()
}

compileTestJava {
    //options.verbose(true)
    options.deprecation(true)
    options.compilerArgs.addAll(
            "-Xlint"
    )
}

jar {
    manifest {
        attributes(
                'Implementation-Title': project.name,
                'Implementation-Version': project.version
        )

    }
    destinationDirectory.set file('build/libs/')
}

task getVersionFromGit() {
    def command = 'git describe --tags'
    def version = command.execute().text.trim()
    println "Application version: ${version}"

    project.version = version
    jar.manifest.attributes['BotCore-Version'] = version

}
tasks.named('build') {
    dependsOn('getVersionFromGit')
}

task copyJar(type: Copy) {
    dependsOn 'jar'
    from jar.getArchiveFile()
    into file('build/libs/')
    rename { String fileName ->
        fileName.replace("-${project.version}", "")
    }
}

dependencyUpdates {
    revision = "dev"
}


java {
    withJavadocJar()
    withSourcesJar()
}
javadoc {
    options.encoding('UTF-8')
}

publishing {
    repositories {
        maven {
            name "reposiliteRepositoryReleases"
            url "http://45.155.169.234:4578/releases"
            allowInsecureProtocol = true
            credentials(PasswordCredentials)
            authentication {
                basic(BasicAuthentication)
            }
        }
    }
    publications {
        maven(MavenPublication) {
            groupId = this.group
            artifactId = this.name
            version = this.version
            from components.java
        }
    }
}