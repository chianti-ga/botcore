plugins {
    id 'java'
    id 'idea'

    //Versioning
    id "org.ajoberstar.reckon" version "0.16.1"
    //Update deps
    id "com.github.ben-manes.versions" version "0.46.0"

    id "io.freefair.lombok" version "8.0.1"

    id 'java-library'
    id 'maven-publish'
}

group 'fr.skitou.botcore'

repositories {
    mavenCentral()
    maven {
        url "https://plugins.gradle.org/m2/"
    }
    maven {
        url 'https://jitpack.io'
    }
}
allprojects {
    dependencies {
        implementation 'com.google.guava:guava:31.1-jre'


        //===Unit tests===


        testImplementation('org.junit.jupiter:junit-jupiter-api:5.9.2')
        testRuntimeOnly('org.junit.jupiter:junit-jupiter-engine:5.9.2')

        testImplementation 'org.mockito:mockito-core:5.2.0'
        // https://mvnrepository.com/artifact/org.mockito/mockito-junit-jupiter
        testImplementation group: 'org.mockito', name: 'mockito-junit-jupiter', version: '5.2.0'

        // === APIs ===
        // JDA
        implementation("net.dv8tion:JDA:5.0.0-beta.10")

        // === Logging ==

        // https://mvnrepository.com/artifact/org.slf4j/slf4j-api
        implementation group: 'org.slf4j', name: 'slf4j-api', version: '2.0.5'
        // https://mvnrepository.com/artifact/ch.qos.logback/logback
        implementation group: 'ch.qos.logback', name: 'logback-classic', version: '1.4.7'

        // Utils
        implementation group: 'org.reflections', name: 'reflections', version: '0.10.2'

        implementation group: 'org.jetbrains', name: 'annotations', version: '24.0.1'
        implementation 'org.apache.commons:commons-lang3:3.12.0'

        implementation 'com.google.code.gson:gson:2.10.1'

        // https://mvnrepository.com/artifact/org.apache.commons/commons-lang3
        implementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.12.0'
        // https://mvnrepository.com/artifact/commons-io/commons-io
        implementation group: 'commons-io', name: 'commons-io', version: '2.11.0'

        //Java Persistence API
        implementation('org.hibernate.orm:hibernate-core:6.2.3.Final')
        implementation 'org.hibernate.orm:hibernate-c3p0:6.2.0.CR3'
        implementation 'org.hibernate.orm:hibernate-community-dialects:6.2.0.CR3'
        implementation group: 'org.xerial', name: 'sqlite-jdbc', version: '3.40.1.0'

        implementation 'com.vdurmont:emoji-java:5.1.1'

        implementation 'io.sentry:sentry:6.16.0'

        implementation group: 'club.minnced', name: 'discord-webhooks', version: '0.8.2'
    }

    idea {
        module {
            downloadJavadoc = true
        }
    }
}

tasks.withType(AbstractArchiveTask) {
    preserveFileTimestamps = false
    reproducibleFileOrder = true
}

tasks.withType(JavaCompile) {
    options.encoding('UTF-8')
}

reckon {
    scopeFromProp()
    stageFromProp('release', 'beta', 'dev')
}

test {
    useJUnitPlatform()
}

compileTestJava {
    //options.verbose(true)
    options.deprecation(true)
    options.compilerArgs.addAll(
            "-Xlint"
    )
}

jar {
    destinationDirectory.set file('build/libs/')
}

task copyJar(type: Copy) {
    dependsOn 'jar'
    from jar.getArchiveFile()
    into file('build/libs/')
    rename { String fileName ->
        fileName.replace("-${project.version}", "")
    }
}

dependencyUpdates {
    revision = "dev"
}

build {

}

java {
    withJavadocJar()
    withSourcesJar()
}
javadoc {
    options.encoding('UTF-8')
}

publishing {
    repositories {
        maven {
            name "reposiliteRepositoryReleases"
            url "http://45.155.169.234:4578/releases"
            allowInsecureProtocol = true
            credentials(PasswordCredentials)
            authentication {
                basic(BasicAuthentication)
            }
        }
    }
    publications {
        maven(MavenPublication) {
            groupId = this.group
            artifactId = this.name
            version = this.version
            from components.java
        }
    }
}
